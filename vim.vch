# Voice mappings for vim (enabled applications )
# 051213: tan modified
# vim.vch
 
# common ranges
<0to9> := 0..9;
<2to9> := 2..9;
<1to20> := 1..20;
<1to30> := 1..30;

# vim versions of text editing Mappings established in vocola general commands
# ---------------------------------------------------------------------------
# Text Editing
#
<direction>             := Left | Right | Up | Down;
<left_right>            := (Left={b} | Right={w});
<start_end>             := (Start=\^ | End=\$);
<top_bottom>             := (Top={Shift+h} | Bottom={Shift+l} | Middle={Shift+m});
#<compass>               := (North={Shift+h} | South={Shift+l} |
#                            East={'$'} | West={0});
<edit_mode>             := (Change={c} | Kill={d} | Copy={y});
<location>              := (Here="i{enter}" | Next="o" | Back="O" |
                            Copy="yyp");

### Characters ( assumes we are in insert mode when calling)
<1to30> <direction>                       =  {$2_$1};
#<edit_mode> (Char | 1 | One)             =  {Esc}{Right}"$1l";
#<edit_mode> Back [1]                     =  {Esc}{Right}"$1h";
#<edit_mode> <1to30> Chars                =  {Esc}{Right}"$2$1l";
#<edit_mode> Back <1to30>                 =  {Esc}{Right}"$2$1h";
(Change={c} | Copy={y}) (Char | 1 | One)  =  {Esc}{Right}"$1l";
(Change={c} | Copy={y}) Back [1]          =  {Esc}{Right}"$1h";
(Change={c} | Copy={y}) <1to30> [Chars]     =  {Esc}{Right}"$2$1l";
(Change={c} | Copy={y}) Back <1to30> [Chars]     =  {Esc}{Right}"$2$1h";
Kill (Char | 1 | One)                     =  {Del}; # {Right}"$1l";
Kill Back [1]                             =  {Left}{Del};
Kill <1to30> [Chars]                      =  {Del_$1};
Kill Back <1to30> [Chars]                 =  {Left_$1}{Del_$1}; #{Esc}{Right}"$2$1h";

### Words
[One] Word                                =  {Esc}{Right}"w";
Back [One] Word                           =  {Esc}{Right}"b";
<1to30> Words                             =  {Esc}{Right}"$1w";
Back <1to30> Words                        =  {Esc}{Right}"$1b";
<edit_mode> Word                          =  {Esc}{Right}"$1w";
<edit_mode> <1to30> Words                 =  {Esc}{Right}"$1$2w";
<edit_mode> Back Word                     =  {Esc}{Right}"$1b";
<edit_mode> Back <1to30> Words            =  {Esc}{Right}"$1$2b";
<edit_mode> Next Word                     =  {Esc}{Right}"w$1w";

### Lines (modify behaviour for deleting lines forward and backward)
Line <start_end>                                            =  {Esc}$1;
Line (Trunc=D | Append=A | Insert=I | Duplicate=yyp)            =  {Esc}$1;
Insert [empty] line <location>                              =  {Esc}$1;
Duplicate Line                                              =  {Esc}"yyp";
Swap Line [(Back=Up | Next=Space)]              =  {esc} When($1, {$1}, "") "ddp";
<edit_mode> Here                                            =  {Esc}$1"\$";
<edit_mode> Back Here                                       =  {Esc}$1"0";
<edit_mode> Start Here                                      =  {Esc}$1"^";
<edit_mode> Line                                            =  {Esc}$1$1;
<edit_mode> <1to30> Lines                                   =  {Esc} Eval($2-1) "$1j";
<edit_mode> Back <1to30> Lines                              =  {Esc} {Up} Eval($2-1) "$1k";
<edit_mode> Next Line                                       =  {Esc}":+1$1"{Enter};
(Change={c} | Copy={y) Back Line                            =  {Esc}"$1k";
Kill Back Line                                              =  {Esc}":-1d"{Enter};
Copy Line To                                                =  {Esc}":.co";
Move Line To                                                =  {Esc}":.m";
                   
### Entire "Flow"
<insert> := (insert=I | append=A);
Flow (Start="gg" | End="G") [<insert>]                      =  {Esc}"$1" Wait(20) "$2";
<edit_mode> Flow Here                                       =  {Esc}":.,\$$1"{Enter};
<edit_mode> Flow All                                        =  {Esc}":0,\$$1"{Enter};
<edit_mode> Back Flow Here                                  =  {Esc}":.,0$1"{Enter};

### Range
Copy Range To                                               =  {Esc}":co"{Left_2};
Move Range                                                  =  {Esc}":m"{Left};
(Indent=">" | Indent Back="<") (Line={enter} | Range={Left})  =  {Esc}":$1"$2;
<edit_mode> Range                                           =  {Esc}":$1"{Left};
(sub | substitute) range                                    =  {Esc}":s///gc"Repeat(6, {Left});

#*-------------------------------------------- */ 
# keyboard mapping for vim commands (distinct global vocola text editing set)
include keys.vch;

# text editing
Blank line (back=O{down} | next=o{up}) = {esc}$1{esc};
Change case <1to30> = {Esc} Repeat($1,"~");
Join Line [(Back=Up | Next=Space)]              =  {esc} When($1, {$1}) "J";

<sub_action> := (line="s" | all="%s" | here=".,\$s" | back here="0,.s");
(sub | substitute) <sub_action>       =  {Esc}":$2///gc"Repeat(4, {Left});

<text_actions> := (
    Paste=p | Paste Back=P | Insert=i | Append=a | Substitute=s | Replace=R | 
    diff push=dp | diff pull=do | change case=~
);
<text_actions> = {esc}$1;

# standard actions
<standard_actions> := (
    Start macro=qz | Repeat macro=@z{Enter} | Format=Q | Normal mode= | 
    visual mode={ctrl+v} 
);
<standard_actions> = {esc}$1;
edit (undo=u | redo={ctrl+r}) [<2to9>] = {esc} When($2, $2$1, 1$1);

# special navigation actions
<navigation_actions> := (
    List changes=":changes" | Previous change="g;" | Undo jump="``" | 
    Return from="''" | Set mark="mz" | Goto mark="'zi" | 
    Beginning previous="-" | Beginning next="+" | Match bracket="%" |
    Scroll [to] bottom="zb" | Scroll [to] top="zt" | Scroll [to] middle="zz"
);
<navigation_actions> = {esc}$1;
Halfpage [(Up="u" | Down="d")]  = {Esc} When($1, {Ctrl+$1}, {Ctrl+d});
Page <top_bottom>               = {Esc}$1;
##next change="g," 


###
### Jump to start of absolute line number L (directly up to 99,999):
###

# stop 1 being recognised as "one" in Vocola
$set numbers 0;

## go to absolute line number $line:
## vim mark line "v" incase we want to return to the original line
GotoLineMod(line, mod) := {Esc}"mv" ":$line"{enter} Wait(500) "$mod"; 
GotoLine4Mod(thousands, hundreds, tens, ones, mod) :=
    GotoLineMod(Eval($thousands*1000 + $hundreds*100 + $tens*10 + $ones), $mod);
<line_mod> := (Go="zz" | Insert="zzI" | Append="zzA"
    # For the following commands we want to perform actions in place, 
    # IE. returns to original line (mark "v").
    | Change=":.c{enter}" "'v" | Kill=":.d{enter}" "'v" 
    | Copy=":.y{enter}" "'v" | beginning="20l" | middle="40l" | end="60l");
line [<line_mod>] <0to9>                      = GotoLineMod($2,$1);
line [<line_mod>] <0to9> <0to9>               = GotoLine4Mod(0, 0, $2, $3, $1);
line [<line_mod>] <0to9> <0to9> <0to9>        = GotoLine4Mod(0, $2, $3, $4, $1);
line [<line_mod>] <0to9> <0to9> <0to9> <0to9> = GotoLine4Mod($2, $3, $4, $5, $1);

Back till <key>                            =  {Esc}"T$1";
Till <key>                                 =  {Esc}"t$1";
Till next                                =  {Esc}";";
Search <_anything>                         =  {Esc}"/\c$1";
Search go <_anything>                      =  {Esc}"/\c$1"{enter};
clear Search                               =  {Esc}"/zzz"{enter};

# commandline actions
Make                                       =  {Esc}":make";
Vim Update Tag File                        =  {Esc}":!ctags -R --c++-kinds=+p --fields=+iaS --extra=+q ."{Enter};
List errors                                =  {Esc}":clist";
Next error                                 =  {Esc}":cn";
Previous error                             =  {Esc}":cp";
Save file                                  =  {Esc}":w"{Enter};
Close file                                 =  {Esc}":q"{Enter};
Save and close file                        =  {Esc}":x"{Enter};
#confirm force close definite              =  {Esc}":q!";
Previous command [<2to9>]                  =  When($1, Repeat($1, {Esc}":"{up}{enter}), {Esc}":"{up});
Edit another                               =  {Esc}":e ";
File browser                               =  {Esc}":e."{Enter};
vim diff split  = {esc}":vert diffsplit ";

# text shortcuts
#Def include                                =  {Esc}"i#include ";
#Def Define                                 =  {Esc}"i#define ";
#Def ifdef                                  =  {Esc}"\pid"{enter};
#file Printf                                  =  {Esc}"i"{Right}"fprintf(,);" {Left_3};
#Equals                                    =  {Esc}i{right}= ";
#Hash                                      =  {Esc}"i#"{esc};
Insert space                               =  {Esc}"i"{space}{esc};
#insert comment [(escape={Esc} | insert="i" | todo="iTODO: ")]  =  {Esc}":TComment "{enter}$1;
Insert space after                         =  {Esc}"i"{Right}{space}{esc};
#Begin comment                              =  {Esc}"i/* ";
End comment                                =  {Esc}"i */"{enter};
#Begin long comment                         =  {Esc}"i#"{esc}"ib"{space};
#End long comment                           =  {Esc}"i#"{esc}"ie"{enter};
Insert line break                          =  {Esc}"i"{enter};
Line comment                               =  {Esc}"i#"{esc}"il"{enter};
Double slash backslash                     =  {Esc}"\/\/";
reference                                  =  "-" Wait(500) ">";
Function definition                        =  {Esc}{Right}"idef ():" {Left_3};
Function definition <_anything>            =  {Esc}{Right}"idef $1():" {Left_2};
Show Help                                  =  {Esc}":help ";
Set setting                                =  {Esc}":set ";
Set dark background =  {Esc}":set bg=dark"{enter};
Set light background =  {Esc}":set bg=light"{enter};

# window navigation
## move created split one page up, return to top of original page in the right split
Split vertical                   =  {Esc}":vsp"{Enter}{Shift+h}{PgUp}
                                    {Ctrl+w}"l";
<window_action> := (left=h | right=l | up=k | down=j | next=w | close=c );
Vim Window <window_action> = {esc}{ctrl+w}"$1";
Vim Window crop            = {Esc}":only"{Enter};
Vim Window make fat        = {esc}":vertical resize +15"{enter};
Vim Window make thin       = {esc}":vertical resize -15"{enter};

# buffer navigation
Previous buffer                  =  {Esc}":bp"{Enter};
Close buffer                     =  {Esc}":bd"{Enter};
Next buffer                      =  {Esc}":bn"{Enter};
Switch buffer                    =  {Esc}":b#"{Enter};
Show buffers                     =  {Esc}":buffers"{enter};
numbered buffer <1to30>          =  {Esc}":b$1"{enter};

# tab navigation
<tab_actions> := (Previous=p | Close=c | Next=n | List=s | Last=l | First=fir);
<tab_actions> tab                 = {Esc}":tab$1"{Enter};
switch tab <1to30>                   = {Esc}":tabn $1"{enter};

# tab navigation
#<tag_actions> := (Previous=":tN{enter}" | Jump="{Ctrl+]}" | Next=":tn{enter}" | 
#                  Match=":ts{enter}" | Return="{Ctrl+t}" | Stack=":tags{enter}");
vim Tag Previous={esc}":tN{enter}";
vim Tag Jump={esc}"{Ctrl+]}";
vim Tag Next={esc}":tn{enter}";
Vim Tag Match={esc}":ts{enter}";
Vim Tag Return={esc}"{Ctrl+t}";
Vim Tag Stack={esc}":tags{enter}";
#Vim Tag <tab_actions>               =  {Esc}$1;

# plug-in actions
Vim Folds                            =  {Esc}{ctrl+f};
Vim show Taglist                     =  {Esc}""{ctrl+g};
Vim Align                            =  {Esc}":Align ";
Vim Align <key>                      =  {Esc}":Align $1"{Left_7};
## omnicomplete
complete                             =  {ctrl+x}{ctrl+o};
local complete                       =  {ctrl+p};
Remove preview = {esc}{ctrl+w}{Up}":q{enter}";
# UNITE plug-in, mappings in .vimrc
#Vim Unite                           =  {Esc}":Unite file_rec buffer"{enter};
Unite files                          =  {Esc}"\f";
Unite files recursive                =  {Esc}"\t";
Unite recent                         =  {Esc}"\m";
#Unite outline                       =  {Esc}"\o";
Unite history = {Esc}"\y";
Unite buffer = {Esc}"\e";
Unite insert = {tab} {Down_3} {enter};

# TComment, note: could also be done with csupport
comment (range=Repeat(9, {Left}) | line={enter}) = {esc}":TComment" $1;       
# csupport (hotkey sections: comment, statements, preprocessor, idioms, 
#           snippets, c++, run)
<comment_type> := (
    frame=fr | function=fu | method=me | class=cl | 
    implementation=fdi | header=fdh | source section=cs |
    header section=hs | keyword=kc | special=sc | date=d | 
    time=t | toggle type=x
);
<comment_type> comment = \c$1;

<statement_type> := (
    do=d | for=fo | if=if | if else=ife | else=e | while=wh | 
    switch=s | case=c
); 
<statement_type> statement = \s$1;

<idiom_type> := (
    function=f | static=sf | main=m | for increment=0 | print=p | 
    file print=fp
); 
<idiom_type> idiom = \i$1;

start (brace="{{enter}" | comment="/*") [(block={enter})] = {esc}{i}"$1$2";

<preprocessor_type> := (
    include header=ih | include global=g | include local=l | define=d 
); 
#<idiom_type> idiom = \i$1;

# NeoBundle
Bundle command (Install | Clean | List)            =  {esc}":NeoBundle$1"{enter};
# editor settings
Nowrap                          = "{Esc}:set nowrap{enter}";
setting based                        = "{Esc}:set paste{enter}";
Edit vim RC                     = {Esc} ":e ~\.vimrc" {Enter};
Reload                          = {Esc} ":source \$MYVIMRC" {Enter};

# histories
command history = {Esc}"q:";
pattern match history = {Esc}"q/";
#
##### surplus for reference     -credit neon?
#### Can be used instead of unimacro "brackets" if ^X characters become a problem
###<surround> := ( par = "()" | bar = "||" | quote = "" );
##Surround <surround> = $1{Left};
